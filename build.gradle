buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'kr.entree.spigradle' version '2.4.3'
    id 'java'
}

sourceCompatibility = 21
targetCompatibility = 21

ext {
    mcVersion = project.property("mcVersion")
}

group project.property("group")

spigot {
    name = project.property("pluginName")
    authors = [project.property("author")]
    apiVersion = project.property("apiVersion")

    commands {
        "secretdoors" {
            description = 'Reloads the config file'
            permissionMessage = 'You don\'t have permissions to do that!'
            aliases  = ['sd']
            usage = '/<command> reload'
        }
    }
    permissions {
        'secretdoors.*' {
            description = 'Gives access to all Secret Doors permissions'
            defaults = 'op'
            children = [
                    'secretdoors.use':true,
                    'secretdoors.reload':true,
                    'secretdoors.create':true
            ]
        }
        'secretdoors.use' {
            description = 'Allows players to open and close Secret Doors'
            defaults = 'op'
        }
        'secretdoors.reload' {
            description = 'Allows players to use /secretdoors reload and /sd reload'
            defaults = 'op'
        }
        'secretdoors.create' {
            description = 'Allows creation of a SecretDoor.'
            defaults = 'op'
        }
    }
    load = STARTUP
    //    depends = ['']
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-parameters"]
}

base {
    archivesName = project.property("pluginName")
}

repositories {
    mavenCentral()
    spigot()
//    bungeecord()
//    paper()
//    protocolLib()
//    enginehub()

}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    compileOnly "org.spigotmc:spigot-api:${mcVersion}-R0.1-SNAPSHOT"
}

shadowJar {
    archiveClassifier.set('')
//    dependencies {
//        include(dependency('co.aikar:acf-paper:0.5.0-SNAPSHOT'))
//    }
//    relocate 'co.aikar.commands', "${packageName}.acf"
//    relocate 'co.aikar.locales', "${packageName}.locales"
}

tasks.build.dependsOn(shadowJar)

processResources {
    project.properties.put("version", this.version)
    expand project.properties
}

defaultTasks 'build'